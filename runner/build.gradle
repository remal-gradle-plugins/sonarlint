apply plugin: 'java-library'
apply plugin: 'maven-publish'

dependencies {
    api 'com.google.code.findbugs:jsr305'

    optional 'org.sonarsource.sonarlint.core:sonarlint-core:8.11.0.56591'
    //optional 'org.sonarsource.java:sonar-java-plugin:7.12.0.29739'
}

publishing.publications.withType(MavenPublication) {
    pom.withXml { XmlProvider xml ->
        Node rootNode = xml.asNode()
        rootNode.remove(rootNode.dependencyManagement)

        rootNode.dependencies.dependency.forEach { Node dependency ->
            String scope = dependency.scope?.text()?.trim()
            if (scope != 'test') {
                dependency.scope.replaceNode {}
            }

            String version = dependency.version?.text()?.trim()
            if (version == null || version.isEmpty()) {
                throw new GradleException("Empty version: $dependency")
            }
        }
    }
}

// Fox some strange reason SonarLint fails for `:runner`, let's disable it:
allprojects {
    pluginManager.withPlugin('java') {
        sourceSets.all { SourceSet sourceSet ->
            tasks.matching { it.name == sourceSet.getTaskName('sonarlint', null) }.all { disableTask(it) }
        }
    }
}
