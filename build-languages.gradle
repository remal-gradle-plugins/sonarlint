import io.github.classgraph.ClassGraph
import io.github.classgraph.ScanResult
import java.nio.file.Paths
import java.util.jar.JarFile

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

buildscript {
    dependencies {
        classpath "io.github.classgraph:classgraph:${rootBuildscriptClasspathVersionOf('io.github.classgraph:classgraph')}"
    }
    repositories {
        mavenCentral()
    }
}

apply plugin: 'name.remal.generate-sources'

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

Map<String, String> defaultLanguageNames = [
    'abap': 'ABAP',
    'apex': 'Apex',
    'c': 'C',
    'cpp': 'C++',
    'cs': 'C#',
    'css': 'CSS',
    'objc': 'Objective C',
    'cobol': 'COBOL',
    'web': 'HTML',
    'ipynb': 'IPython',
    'java': 'Java',
    'jcl': 'JCL',
    'js': 'JavaScript',
    'kotlin': 'Kotlin',
    'php': 'PHP',
    'pli': 'PL/I',
    'plsql': 'PL/SQL',
    'py': 'Python',
    'rpg': 'RPG',
    'ruby': 'Ruby',
    'scala': 'Scala',
    'secrets': 'Secrets',
    'swift': 'Swift',
    'tsql': 'T-SQL',
    'ts': 'TypeScript',
    'jsp': 'JSP',
    'vbnet': 'VB.NET',
    'xml': 'XML',
    'yaml': 'YAML',
    'json': 'JSON',
    'go': 'Go',
    'cloudformation': 'CloudFormation',
    'docker': 'Docker',
    'kubernetes': 'Kubernetes',
    'terraform': 'Terraform',
    'azureresourcemanager': 'AzureResourceManager',
]

@groovy.transform.Immutable
class PluginLanguageInfo {
    String key
    String name
    String minNodeJsVersion
}

@groovy.transform.Immutable
class LanguageInfo {
    String key
    String name
    List<String> defaultFileSuffixes
    String fileSuffixesPropKey
    String minNodeJsVersion
}


Configuration sonarPluginLanguagesConf = configurations.create('sonarPluginLanguages') { Configuration conf ->
    conf.canBeResolved = true
    conf.canBeConsumed = false

    conf.dependencies.withType(ModuleDependency).configureEach { dep ->
        dep.attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage.class, Usage.JAVA_RUNTIME))
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category.class, Category.LIBRARY))
        }
    }

    conf.dependencies.addAllLater(
        provider {
            project.configurations.sonarCoreArtifacts.dependencies
                .collect { project.dependencies.create("${it.group}:${it.name}:${it.version}") }
        }
    )
    conf.dependencies.addAllLater(
        provider {
            project.configurations.sonarPluginArtifacts.dependencies
                .collect { project.dependencies.create("${it.group}:${it.name}:${it.version}") }
                .collect { it.transitive = false; it }
        }
    )
}

ListProperty<List<PluginLanguageInfo>> pluginLanguageInfos = rootProject.ext.pluginLanguageInfos = project.objects.listProperty(PluginLanguageInfo).value(provider {
    Collection<File> classpathFiles = sonarPluginLanguagesConf.files
    Collection<URL> classpathUrls = classpathFiles.collect { it.toURI().toURL() }
    new URL("jar", "", "file:test.jar!/resource.txt").openConnection().setDefaultUseCaches(false)
    new URLClassLoader(classpathUrls.toArray(new URL[0]), Logger.classLoader).withCloseable { classLoader ->
        List<String> languageClassNames = new ClassGraph()
            .overrideClassLoaders(classLoader)
            .filterClasspathElementsByURL { classpathUrls.contains(it) }
            .enableClassInfo()
            .scan()
            .withCloseable { ScanResult scanResult ->
                scanResult.getClassesImplementing('org.sonar.api.resources.Language')
                    .findAll { !it.interface && !it.abstract }
                    .findAll { it.subclasses.isEmpty() }
                    .collect { it.name }
            }

        Class<?> configurationClass = Class.forName('org.sonar.api.config.Configuration', true, classLoader)

        Object emptyConfiguration = Class.forName('org.sonarsource.sonarlint.core.rule.extractor.EmptyConfiguration', true, classLoader)
            .getConstructor()
            .newInstance()

        languageClassNames
            .collect { String languageClassName ->
                Class<?> languageClass = Class.forName(languageClassName, true, classLoader)
                Object language = null
                try {
                    language = languageClass.getConstructor().newInstance()
                } catch (NoSuchMethodException ignored) {
                    // do nothing
                }
                try {
                    language = languageClass.getConstructor(configurationClass).newInstance(emptyConfiguration)
                } catch (NoSuchMethodException ignored) {
                    // do nothing
                }
                if (language == null) {
                    throw new GradleException("Can't instantiate $languageClass")
                }

                URL languageJarUrl = language.class.protectionDomain?.codeSource?.location
                if (languageJarUrl == null) {
                    throw new GradleException("No code source location found for ${language.class}")
                }
                File languageJarFile = Paths.get(languageJarUrl.toURI()).toFile()
                String minNodeJsVersion = new JarFile(languageJarFile).withCloseable { jarFile ->
                    jarFile.manifest?.mainAttributes?.getValue('NodeJs-Min-Version')?.toString()
                }

                new PluginLanguageInfo(
                    key: language.key,
                    name: language.name,
                    minNodeJsVersion: minNodeJsVersion?.length() ? minNodeJsVersion : null,
                )
            }
            .toSorted { it.key }
    }
}).with { it.finalizeValueOnRead(); it }


Configuration sonarAllLanguagesConf = configurations.create('sonarAllLanguages') { Configuration conf ->
    conf.canBeResolved = true
    conf.canBeConsumed = false

    conf.dependencies.withType(ModuleDependency).configureEach { dep ->
        dep.attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage.class, Usage.JAVA_RUNTIME))
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category.class, Category.LIBRARY))
        }
    }

    conf.dependencies.addAllLater(
        provider {
            project.configurations.sonarCoreArtifacts.dependencies
                .collect { project.dependencies.create("${it.group}:${it.name}:${it.version}") }
        }
    )
}

ListProperty<List<LanguageInfo>> allLanguageInfos = rootProject.ext.allLanguageInfos = project.objects.listProperty(LanguageInfo).value(provider {
    Collection<File> classpathFiles = sonarAllLanguagesConf.files
    Collection<URL> classpathUrls = classpathFiles.collect { it.toURI().toURL() }
    new URL("jar", "", "file:test.jar!/resource.txt").openConnection().setDefaultUseCaches(false)
    new URLClassLoader(classpathUrls.toArray(new URL[0]), Logger.classLoader).withCloseable { classLoader ->
        Class<?> languageClass = Class.forName('org.sonarsource.sonarlint.core.commons.api.SonarLanguage', true, classLoader)
        List<LanguageInfo> infos = languageClass.enumConstants.collect()
            .collect { language ->
                return new LanguageInfo(
                    key: language.name(),
                    name: pluginLanguageInfos.get().find { it.key == language.sonarLanguageKey }?.name
                        ?: defaultLanguageNames[language.sonarLanguageKey]
                        ?: language.sonarLanguageKey,
                    defaultFileSuffixes: language.defaultFileSuffixes.collect(),
                    fileSuffixesPropKey: language.fileSuffixesPropKey,
                    minNodeJsVersion: pluginLanguageInfos.get().find { it.key == language.sonarLanguageKey }?.minNodeJsVersion,
                )
            }

        return infos
    }
}).with { it.finalizeValueOnRead(); it }


rootProject.ext.requiringNodeJsLanguagesString = project.objects.property(String).value(provider {
    allLanguageInfos.get()
        .findAll { it.minNodeJsVersion != null }
        .collect { it.name }
        .join(", ")
}).with { it.finalizeValueOnRead(); it }


generateSources.forMainSourceSet.java {
    inputs.files(sonarPluginLanguagesConf).withPropertyName('sonarPluginLanguagesConf').optional()
    inputs.files(sonarAllLanguagesConf).withPropertyName('sonarAllLanguagesConf').optional()

    classFile(project.calculateBaseJavaPackage() + '.internal', 'SonarLanguage') {
        addStaticImport("lombok.AccessLevel", "PRIVATE")

        addImport("java.util.List")
        addImport("javax.annotation.Nullable")
        addImport("lombok.Getter")
        addImport("lombok.RequiredArgsConstructor")
        addImport("org.jetbrains.annotations.Unmodifiable")

        line("@Getter")
        line("@RequiredArgsConstructor(access = PRIVATE)")
        line("@SuppressWarnings(\"all\")")
        block("public enum ${simpleName}") {
            allLanguageInfos.get().forEach { LanguageInfo info ->
                line("${info.key}(")
                line("    \"${escapeString(info.name)}\",")
                line("    List.of(${info.defaultFileSuffixes.collect { suffix -> '"' + escapeString(suffix) + '"' }.join(', ')}),")
                if (info.fileSuffixesPropKey.isEmpty() || info.fileSuffixesPropKey.find(/[^\w.]/)) {
                    line("    null,")
                } else {
                    line("    \"${escapeString(info.fileSuffixesPropKey)}\",")
                }
                if (info.minNodeJsVersion != null) {
                    line("    \"%s\"", escapeString(info.minNodeJsVersion))
                } else {
                    line("    null")
                }
                line("),")
            }
            line(";")
            line("")
            line("private final String name;")
            line("private final @Unmodifiable List<String> defaultFileSuffixes;")
            line("private final @Nullable String fileSuffixesPropKey;")
            line("private final @Nullable String minNodeJsVersion;")

            line()
            block("public boolean isRequireNodeJs()") {
                line("return minNodeJsVersion != null;")
            }
        }
    }
}
