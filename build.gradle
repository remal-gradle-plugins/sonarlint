import static java.nio.charset.StandardCharsets.UTF_8

import java.nio.file.Files
import java.util.zip.ZipEntry
import java.util.zip.ZipInputStream
import java.util.zip.ZipOutputStream
import org.objectweb.asm.ClassReader
import org.objectweb.asm.ClassWriter
import org.objectweb.asm.tree.ClassNode

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

buildscript {
    String rootGroupId = project.ext.rootGroupId = "name.remal.gradle-plugins.${rootProject.name}"
    String rootArtifactId = project.ext.rootArtifactId = rootProject.name
    String rootSnapshotVersion = project.ext.rootSnapshotVersion = '5-SNAPSHOT'
    dependencies {
        //classpath("$rootGroupId:$rootArtifactId:$rootSnapshotVersion") { version { strictly(rootSnapshotVersion) } }
        classpath 'name.remal.gradle-plugins.toolkit:build-logic:0.71.1'
        classpath 'de.undercouch:gradle-download-task:5.6.0'
        classpath 'com.tisonkun.os:os-detector-core:0.6.0'
        classpath 'io.github.classgraph:classgraph:4.8.179'

        classpath platform("org.ow2.asm:asm-bom:9.7.1")
        classpath 'org.ow2.asm:asm-tree'
    }
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    project.ext.rootBuildscriptClasspathVersionOf = { String notation ->
        String version = project.buildscript.configurations.classpath
            .resolvedConfiguration
            .lenientConfiguration
            .allModuleDependencies
            .find { "${it.moduleGroup}:${it.moduleName}" == notation }
            ?.moduleVersion
        if (version == null) {
            throw new GradleException("Classpath dependency version can't be found for '${notation}'")
        }
        return version
    }.memoize()
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

allprojects {
    group = project.rootGroupId
    version = project.rootSnapshotVersion
}

apply plugin: 'name.remal.toolkit.build-logic'

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

allprojects {
    pluginManager.withPlugin('name.remal.sonarlint') {
        sonarLint {
            boolean hasCurrentSnapshotBuildscriptDependency = rootProject.buildscript.configurations.classpath.dependencies
                .any { "${it.group}:${it.name}:${it.version}" == "$rootGroupId:$rootArtifactId:$rootSnapshotVersion" }
            if (hasCurrentSnapshotBuildscriptDependency) {
                fork.enabled = false
            }
        }
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

project.registerCrossCompileDependencyInfo(
    [
        name: 'sonarlint',
        maxVersionNumbers: 3,
        dependencyNotations: [
            'org.sonarsource.sonarlint.core:sonarlint-core',
        ],
    ]
)

allprojects {
    pluginManager.withPlugin('java') {
        dependencies {
            optional 'name.remal.gradle-api:gradle-api'
        }
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

apply plugin: 'java-gradle-plugin'
apply plugin: 'name.remal.classes-relocation'
apply plugin: 'name.remal.generate-sources'

dependencies {
    classesRelocation 'com.tisonkun.os:os-detector-core:0.6.0'

    optional('org.sonarsource.sonarlint.core:sonarlint-core:10.13.0.79996') {
        exclude group: 'com.google.guava'
    }
}

gradlePlugin {
    plugins {
        'name.remal.sonarlint' {
            id = 'name.remal.sonarlint'
            implementationClass = 'name.remal.gradle_plugins.sonarlint.SonarLintPlugin'
            displayName = 'SonarLint'
            description = property('repository-description')
        }
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

generateSources.forMainSourceSet.java {
    Provider<MavenPublication> publication = provider { publishing.publications.withType(MavenPublication).pluginMaven }
    Provider<String> group = provider { publication.get().groupId }
    Provider<String> artifactId = provider { publication.get().artifactId }
    Provider<String> version = provider { publication.get().version }
    inputs.property('groupId', group)
    inputs.property('artifactId', artifactId)
    inputs.property('version', version)

    classFile(project.calculateBaseJavaPackage(), 'SonarLintPluginBuildInfo') {
        block("interface ${simpleName}") {
            line("String SONARLINT_PLUGIN_ID = \"${escapeString(gradlePlugin.plugins.names.first())}\";")
            line("String SONARLINT_PLUGIN_GROUP = \"${escapeString(group.get())}\";")
            line("String SONARLINT_PLUGIN_ARTIFACT_ID = \"${escapeString(artifactId.get())}\";")
            line("String SONARLINT_PLUGIN_VERSION = \"${escapeString(version.get())}\";")
        }
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

project.redefineDependencyTargetJvmVersion('org.sonarsource.git.blame:git-files-blame', 8)

Configuration allSonarArtifactsConf = project.configurations.create('allSonarArtifacts')

Map<String, String> nonReproducibleSonarDependencyVersions = new TreeMap<>().asSynchronized()
allSonarArtifactsConf.resolutionStrategy.eachDependency { DependencyResolveDetails details ->
    ModuleVersionSelector target = details.target

    String version = target.version
    if (version == null) {
        return
    }
    boolean isNotReproducible = version.endsWith('-SNAPSHOT') || ['(', '[', ']', ')'].any { version.contains(it) }
    if (!isNotReproducible) {
        return
    }

    String id = "${target.group}:${target.name}".toString()
    nonReproducibleSonarDependencyVersions.put(id, version)
}

Closure<Configuration> createSonarArtifactsConfiguration = { String name, boolean addToIndirectApi = true ->
    return project.configurations.create(name) { Configuration conf ->
        conf.transitive = false
        conf.extendsFrom = []
        conf.dependencies.all { Dependency dep ->
            allSonarArtifactsConf.dependencies.add(dep.copy())

            if (dep instanceof ModuleDependency) {
                dep.transitive = false
            }
            if (dep instanceof ExternalDependency) {
                dep.version { strictly(dep.version) }
            }
        }

        if (addToIndirectApi) {
            project.configurations.indirectApi.extendsFrom(conf)
        }
    }
}

createSonarArtifactsConfiguration('sonarCoreArtifacts')
createSonarArtifactsConfiguration('sonarPluginArtifacts')

dependencies {
    // Core:
    sonarCoreArtifacts 'org.sonarsource.sonarlint.core:sonarlint-core:10.13.0.79996'


    // Plugins:
    // See full list of supported plugins here: https://github.com/SonarSource/sonarlint-intellij/blob/master/gradle/libs.versions.toml
    sonarPluginArtifacts 'org.sonarsource.java:sonar-java-symbolic-execution-plugin:8.9.0.37768'
    sonarPluginArtifacts 'org.sonarsource.java:sonar-java-plugin:8.9.0.37768'
    sonarPluginArtifacts 'org.sonarsource.javascript:sonar-javascript-plugin:10.20.0.29356'
    //sonarPluginArtifacts 'org.sonarsource.php:sonar-php-plugin:3.39.0.12526' // PHP projects are unlikely built with Gradle
    //sonarPluginArtifacts 'org.sonarsource.python:sonar-python-plugin:4.23.0.17664' // Python projects are unlikely built with Gradle
    sonarPluginArtifacts 'org.sonarsource.kotlin:sonar-kotlin-plugin:2.22.0.5972'
    //sonarPluginArtifacts 'org.sonarsource.slang:sonar-ruby-plugin:1.17.1.134' // Ruby projects are unlikely built with Gradle
    sonarPluginArtifacts 'org.sonarsource.html:sonar-html-plugin:3.18.0.5605'
    sonarPluginArtifacts 'org.sonarsource.xml:sonar-xml-plugin:2.12.0.5749'
    //sonarPluginArtifacts 'org.sonarsource.sonarlint.omnisharp:sonarlint-omnisharp-plugin:1.25.0.100205' // OmniSharp projects are unlikely built with Gradle
    sonarPluginArtifacts 'org.sonarsource.text:sonar-text-plugin:2.20.0.5038'
    //sonarPluginArtifacts 'org.sonarsource.slang:sonar-go-plugin:1.17.1.145' // Go projects are unlikely built with Gradle
    sonarPluginArtifacts 'org.sonarsource.iac:sonar-iac-plugin:1.41.0.14206'
    //sonarPluginArtifacts 'com.sonarsource.cpp:sonar-cfamily-plugin:6.60.0.76379' // C++ projects are unlikely built with Gradle
    //sonarPluginArtifacts 'org.sonarsource.dotnet:sonar-csharp-plugin:10.2.0.103721' // C# projects are unlikely built with Gradle
    //sonarPluginArtifacts 'com.sonarsource.dotnet:sonar-csharp-enterprise-plugin:10.2.0.103721' // C# projects are unlikely built with Gradle
    sonarPluginArtifacts 'org.sonarsource.slang:sonar-scala-plugin:1.18.0.266'
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

project.ext.DownloadTask = Class.forName('de.undercouch.gradle.tasks.download.Download')
apply from: 'build-nodejs.gradle'
apply from: 'build-languages.gradle'

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

generateSources.forMainSourceSet.java { Task task ->
    inputs.files(allSonarArtifactsConf).withPropertyName(allSonarArtifactsConf.name + 'Conf').optional()

    classFile(project.calculateBaseJavaPackage(), 'SonarDependencies') {
        addStaticImport("lombok.AccessLevel", "PRIVATE")

        addImport("com.google.common.collect.ImmutableMap")
        addImport("java.util.Map")
        addImport("lombok.NoArgsConstructor")

        line("@NoArgsConstructor(access = PRIVATE)")
        block("abstract class ${simpleName}") {
            line("")
            line("private static final Map<String, SonarDependency> MAPPING = ImmutableMap.<String, SonarDependency>builder()")
            indent {
                configurations.sonarCoreArtifacts.dependencies.forEach { Dependency dep ->
                    line(".put(\"${escapeString(dep.name)}\", SonarDependency.builder()")
                    line("    .type(SonarDependencyType.CORE)")
                    line("    .group(\"${escapeString(dep.group)}\")")
                    line("    .name(\"${escapeString(dep.name)}\")")
                    line("    .version(\"${escapeString(dep.version)}\")")
                    line("    .build()")
                    line(")")
                }
                configurations.sonarPluginArtifacts.dependencies.forEach { Dependency dep ->
                    line(".put(\"${escapeString(dep.name)}\", SonarDependency.builder()")
                    line("    .type(SonarDependencyType.PLUGIN)")
                    line("    .group(\"${escapeString(dep.group)}\")")
                    line("    .name(\"${escapeString(dep.name)}\")")
                    line("    .version(\"${escapeString(dep.version)}\")")
                    line("    .build()")
                    line(")")
                }
                line(".build();")
            }

            line("")
            block("public static Map<String, SonarDependency> getSonarDependencies()") {
                line("return MAPPING;")
            }

            line("")
            block("public static SonarDependency getSonarDependency(String id)") {
                line("var dependency = getSonarDependencies().get(id);")
                block("if (dependency == null)") {
                    line("throw new IllegalStateException(\"Sonar dependency is not registered: \" + id);")
                }
                line("return dependency;")
            }
        }
    }

    classFile(project.calculateBaseJavaPackage(), 'ResolvedNonReproducibleSonarDependencies') {
        addStaticImport("lombok.AccessLevel", "PRIVATE")

        addImport("com.google.common.collect.ImmutableMap")
        addImport("java.util.Map")
        addImport("javax.annotation.Nullable")
        addImport("lombok.NoArgsConstructor")

        line("@NoArgsConstructor(access = PRIVATE)")
        block("abstract class ${simpleName}") {
            line("")
            line("private static final Map<String, SonarDependency> MAPPING = ImmutableMap.<String, SonarDependency>builder()")
            indent {
                allSonarArtifactsConf.resolvedConfiguration.lenientConfiguration.allModuleDependencies.forEach { ResolvedDependency dep ->
                    String depId = "${dep.moduleGroup}:${dep.moduleName}".toString()
                    String nonReproducibleVersion = nonReproducibleSonarDependencyVersions[depId]
                    if (nonReproducibleVersion != null) {
                        line(".put(\"${escapeString("${depId}:${nonReproducibleVersion}")}\", SonarDependency.builder()")
                        line("    .type(SonarDependencyType.OTHER)")
                        line("    .group(\"${escapeString(dep.moduleGroup)}\")")
                        line("    .name(\"${escapeString(dep.moduleName)}\")")
                        line("    .version(\"${escapeString(dep.moduleVersion)}\")")
                        line("    .build()")
                        line(")")
                    }
                }
                line(".build();")
            }

            line("")
            block("public static Map<String, SonarDependency> getResolvedNonReproducibleSonarDependencies()") {
                line("return MAPPING;")
            }

            line("")
            line("@Nullable")
            block("public static SonarDependency getResolvedNonReproducibleSonarDependency(String notation)") {
                line("return getResolvedNonReproducibleSonarDependencies().get(notation);")
            }
        }
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

tasks.named('processReadme') {
    ListProperty pluginLanguageInfos = rootProject.pluginLanguageInfos
    readmeFileProcessors.add(
        { File readmeFile ->
            String content = readmeFile.getText('UTF-8')

            content = content.replaceAll(
                /<!--\s*(sonar-plugins-list)\s*-->[\s\S]*?<!--\s*\/\s*\1\s*-->/
            ) {
                String tag = it[1]
                logger.lifecycle('  Handling {}', tag)
                StringBuilder listContent = new StringBuilder()

                Map<String, String> languageDocPaths = [
                    'js': 'javascript',
                    'ts': 'typescript',
                ]

                pluginLanguageInfos.get()
                    .toSorted { it.key }
                    .forEach { info ->
                        String languageDocPath = languageDocPaths[info.key] ?: info.key
                        URL url = new URL("https://rules.sonarsource.com/${languageDocPath}/")
                        HttpURLConnection connection = url.openConnection()
                        connection.useCaches = false
                        connection.connectTimeout = 5_000
                        connection.readTimeout = 60_000
                        connection.connect()
                        try {
                            int responseCode = connection.responseCode
                            if (responseCode == 200) {
                                listContent.append("\n* [${info.name}]($url)")
                            } else if (responseCode == 404) {
                                listContent.append("\n* ${info.name}")
                            } else {
                                throw new GradleException("$url returned unexpected status: $responseCode")
                            }

                        } finally {
                            connection.disconnect()
                        }
                    }

                return "<!--$tag-->\n$listContent\n\n<!--/$tag-->"
            }

            readmeFile.setText(content, 'UTF-8')
        }
    )
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

allprojects {
    /*
    if (gradle.startParameter.taskNames.isEmpty()
        || gradle.startParameter.taskNames == ['processIdeaSettings']
        || gradle.startParameter.taskNames == ['dependencies']
        || gradle.startParameter.taskNames == ['help']
    ) {
        return
    }
    */

    Attribute<String> artifactType = Attribute.of('artifactType', String)
    Attribute<Boolean> unprocessedSonar = Attribute.of('unprocessedSonar', Boolean)
    dependencies {
        attributesSchema {
            attribute(unprocessedSonar)
        }
        artifactTypes.getByName('jar') {
            attributes.attribute(unprocessedSonar, false)
        }
    }

    configurations
        .matching { !it.name.startsWith('sonar') }
        .matching { !it.name.startsWith('allSonar') }
        .all { Configuration conf ->
            if (conf.state != Configuration.State.UNRESOLVED
                || !conf.canBeResolved
            ) {
                return
            }

            Closure<Boolean> isSonarGroup = { String group ->
                return group == 'org.sonarsource'
                    || group.startsWith('org.sonarsource.')
            }

            conf.dependencies.withType(ExternalModuleDependency).configureEach { ExternalModuleDependency dep ->
                if (isSonarGroup(dep.group)) {
                    dep.exclude(group: 'com.google.guava')
                }
            }

            conf.resolutionStrategy.dependencySubstitution { DependencySubstitutions substitutions ->
                substitutions.all { DependencySubstitution substitution ->
                    if (!(substitution.requested instanceof ModuleComponentSelector)) {
                        return
                    }

                    ModuleComponentSelector requested = (ModuleComponentSelector) substitution.requested
                    if (isSonarGroup(requested.group)) {
                        ComponentSelector target = substitutions.variant(requested) { VariantSelectionDetails details ->
                            details.attributes {
                                attribute(unprocessedSonar, true)
                            }
                        }
                        substitution.useTarget(target)
                    }
                }
            }
        }

    dependencies {
        registerTransform(ProcessSonar) {
            from.attribute(unprocessedSonar, false).attribute(artifactType, 'jar')
            to.attribute(unprocessedSonar, true).attribute(artifactType, 'jar')
        }
    }
}

abstract class ProcessSonar implements TransformAction<ProcessSonarParameters> {

    interface ProcessSonarParameters extends TransformParameters {}

    @InputArtifact
    abstract Provider<FileSystemLocation> getInputArtifact()

    @Override
    void transform(TransformOutputs outputs) {
        File file = inputArtifact.get().asFile
        //println file
        File outputFile = outputs.file(file.name)
        if (outputFile.exists() && outputFile.lastModified() >= rootProject.buildFile.lastModified()) {
            return
        }

        Files.createDirectories(outputFile.parentFile.toPath())
        new ZipInputStream(new FileInputStream(file), UTF_8).withCloseable { ZipInputStream inputStream ->
            new ZipOutputStream(new FileOutputStream(outputFile), UTF_8).withCloseable { ZipOutputStream outputStream ->
                while (true) {
                    ZipEntry zipEntry = inputStream.nextEntry
                    if (zipEntry == null) {
                        break
                    }

                    if (zipEntry.directory) {
                        continue
                    }

                    if (zipEntry.name.contains('/shaded/')) {
                        continue
                    }

                    if (!zipEntry.name.contains('sonar')
                        && !zipEntry.name.startsWith('META-INF/')
                    ) {
                        continue
                    }

                    outputStream.putNextEntry(zipEntry)

                    if (!zipEntry.name.endsWith('.class')) {
                        outputStream << inputStream
                        continue
                    }

                    if (!zipEntry.name.startsWith('org/sonarsource/')
                        && !zipEntry.name.startsWith('org/sonar/')
                    ) {
                        outputStream << inputStream
                        continue
                    }


                    ClassNode classNode = new ClassNode()
                    new ClassReader(inputStream).accept(classNode, 0)

                    Closure processDeprecatedAnnotations = { node ->
                        // remove parameters of @Deprecated
                        node.visibleAnnotations
                            ?.findAll { it.desc == 'Ljava/lang/Deprecated;' && it.values?.size() }
                            ?.forEach {
                                it.values = []
                                //println classNode.name
                            }
                    }
                    processDeprecatedAnnotations(classNode)
                    classNode.fields?.forEach { processDeprecatedAnnotations(it) }
                    classNode.methods?.forEach { processDeprecatedAnnotations(it) }

                    ClassWriter classWriter = new ClassWriter(0)
                    classNode.accept(classWriter)

                    outputStream << classWriter.toByteArray()
                }
            }
        }
    }

}
